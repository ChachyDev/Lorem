plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.10" apply false
}

group = "club.chachy"
version = "0.1.1"

allprojects {
    apply plugin: "maven-publish"
    apply plugin: "org.jetbrains.kotlin.jvm"

    repositories {
        mavenCentral()
    }

    publishing {
        repositories {
            maven {
                url = "$buildDir/repository"
            }
        }

        publications {
            //noinspection GroovyAssignabilityCheck
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = "Lorem"
                    url = "https://github.com/ChachyDev/Lorem"

                    developers {
                        developer {
                            id = "lorem"
                            name = "ChachyDev"
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:2.14.0"
    implementation "org.apache.logging.log4j:log4j-core:2.14.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "io.ktor:ktor-client-gson:1.5.4"
    implementation "io.ktor:ktor-client-core:1.5.4"
    implementation "io.ktor:ktor-client-apache:1.5.4"

    implementation project(":lib:authentication:mojang-auth")
    implementation project(":lib:authentication:microsoft-auth")

    implementation project(":lib:api:service")
}

compileKotlin {
    kotlinOptions.jvmTarget = 1.8
}

compileTestKotlin {
    kotlinOptions.jvmTarget = 1.8
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}